' Flag of Canada
' Copyright Â© 2020 Nonki Takahashi.  The MIT License.
' Using LitDev Extension
' Program ID MMC323

GraphicsWindow.Title = "Flag of Canada"
SB_Workaround()
scale = 0.06
GraphicsWindow.Width = 9600 * scale
GraphicsWindow.Height = 4800 * scale
Shapes_Init_Canada()
angle = 0
name = "Canada"
Sprite_Add()

Sub SB_RotateWorkaround
  ' Small Basic | rotate workaround For Silverlight
  ' param shp - current shape
  ' param x, y - original coordinate
  ' param _alpha - angle [radian]
  ' returns x, y - workaround coordinate
  If shp["func"] = "tri" Then
    x1 = -Math.Floor(shp["x3"] / 2)
    y1 = -Math.Floor(shp["y3"] / 2)
  ElseIf shp["func"] = "line" Then
    x1 = -Math.Floor(Math.Abs(shp["x1"] - shp["x2"]) / 2)
    y1 = -Math.Floor(Math.Abs(shp["y1"] - shp["y2"]) / 2)
  EndIf
  ox = x - x1
  oy = y - y1
  x = x1 * Math.Cos(_alpha) - y1 * Math.Sin(_alpha) + ox
  y = x1 * Math.Sin(_alpha) + y1 * Math.Cos(_alpha) + oy
EndSub

Sub SB_Workaround
  ' Small Basic | workaround for Silverlight / SBD
  ' return silverlight - "True" if in remote
  ' return sbd - "True" if Small Basic Desktop
  color = GraphicsWindow.GetPixel(0, 0)
  sbd = "False"
  If Text.GetLength(color) > 7 Then
    silverlight = "True"
    msWait = 300
  Else
    silverlight = "False"
    _gw = GraphicsWindow.Width
    _gh = GraphicsWindow.Height
    If (_gw = 624) And (_gh = 441) Then
      sbd = "True"
    EndIf
  EndIf
EndSub

Sub Shapes_CalcWidthAndHeight
  ' Shapes | calculate total width and height of shapes
  ' param shape[] - shape array
  ' return shWidth, shHeight - total size of shapes
  For i = 1 To Array.GetItemCount(shape)
    shp = shape[i]
    If shp["func"] = "tri" Or shp["func"] = "line" Then
      xmin = shp["x1"]
      xmax = shp["x1"]
      ymin = shp["y1"]
      ymax = shp["y1"]
      If shp["x2"] < xmin Then
        xmin = shp["x2"]
      EndIf
      If xmax < shp["x2"] Then
        xmax = shp["x2"]
      EndIf
      If shp["y2"] < ymin Then
        ymin = shp["y2"]
      EndIf
      If ymax < shp["y2"] Then
        ymax = shp["y2"]
      EndIf
      If shp["func"] = "tri" Then
        If shp["x3"] < xmin Then
          xmin = shp["x3"]
        EndIf
        If xmax < shp["x3"] Then
          xmax = shp["x3"]
        EndIf
        If shp["y3"] < ymin Then
          ymin = shp["y3"]
        EndIf
        If ymax < shp["y3"] Then
          ymax = shp["y3"]
        EndIf
      EndIf
      shp["width"] = xmax - xmin
      shp["height"] = ymax - ymin
    ElseIf shp["func"] = "poly" Then
      pts = LDText.Split(shp["pt"], " ")
      nPts = Array.GetItemCount(pts)
      _pt = LDText.Split(pts[1], ",")
      xmin = _pt[1]
      xmax = _pt[1]
      ymin = _pt[2]
      ymax = _pt[2]
      For iPts = 2 To nPts
        _pt = LDText.Split(pts[1], ",")
        If _pt[1] < xmin Then
          xmin = _pt[1]
        EndIf
        If xmax < _pt[1] Then
          xmax = _pt[1]
        EndIf
        If _pt[2] < ymin Then
          ymin = _pt[2]
        EndIf
        If ymax < _pt[2] Then
          ymax = _pt[2]
        EndIf
      EndFor
      shp["width"] = xmax - xmin
      shp["height"] = ymax - ymin
    EndIf
    If i = 1 Then
      shWidth = shp["x"] + shp["width"]
      shHeight = shp["y"] + shp["height"]
    Else
      If shWidth < shp["x"] + shp["width"] Then
        shWidth = shp["x"] + shp["width"]
      EndIf
      If shHeight < shp["y"] + shp["height"] Then
        shHeight = shp["y"] + shp["height"]
      EndIf
    EndIf
    shape[i] = shp
  EndFor
EndSub

Sub Shapes_Init_Canada
  ' Shapes | initialize sample shapes data
  ' return shX, shY - current position of shapes
  ' return shape - array of shapes
  shX = 0   ' x offset
  shY = 0   ' y offset
  shape = ""
  shape[1] = "func=poly;pt=0,0 2400,0 2499,99 7101,99 7200,0 9600,0 9600,4800 7200,4800 7101,4701 2499,4701 2400,4800 0,4800;pc=Transparent;bc=#da291c;"
  shape[2] = "func=poly;pt=2400,0 7200,0 7200,4800 2400,4800;pc=Transparent;bc=#fff;"
  shape[3] = "func=poly;pt=4890,4430 4845,3567 4845,3567 4845,3567 4845.68,3550.45 4849.23,3534.28 4855.53,3518.96 4864.39,3504.98 4875.55,3492.74 4888.66,3482.63 4903.33,3474.95 4919.11,3469.93 4935.52,3467.73 4952.07,3468.41 4956,3469 5815,3620 5699,3300 5815,3620 5699,3300 5699,3300 5696.01,3289.07 5694.97,3277.79 5695.9,3266.5 5698.78,3255.54 5703.51,3245.25 5709.97,3235.93 5717.94,3227.88 5719,3227 6660,2465 6448,2366 6660,2465 6448,2366 6448,2366 6438.22,2360.28 6429.57,2352.96 6422.34,2344.24 6416.72,2334.4 6412.9,2323.73 6410.99,2312.57 6411.05,2301.23 6413.07,2290.09 6414,2287 6600,1715 6058,1830 6600,1715 6058,1830 6058,1830 6046.75,1831.34 6035.44,1830.7 6024.41,1828.11 6013.99,1823.64 6004.52,1817.43 5996.26,1809.67 5989.48,1800.6 5985,1792 5880,1545 5457,1999 5880,1545 5457,1999 5457,1999 5448.52,2006.51 5438.86,2012.44 5428.32,2016.59 5417.22,2018.86 5405.89,2019.16 5394.69,2017.49 5383.94,2013.9 5373.98,2008.5 5365.11,2001.45 5357.6,1992.97 5351.67,1983.31 5347.52,1972.77 5345.25,1961.67 5344.95,1950.34 5346,1942 5550,890 5223,1079 5550,890 5223,1079 5223,1079 5212.76,1083.86 5201.84,1086.86 5190.56,1087.92 5179.26,1087.01 5168.3,1084.15 5158,1079.42 5148.68,1072.99 5140.61,1065.03 5134.05,1055.79 5132,1052 4800,400 4468,1052 4800,400 4468,1052 4468,1052 4462.07,1061.65 4454.55,1070.13 4445.67,1077.17 4435.71,1082.56 4424.96,1086.15 4413.75,1087.81 4402.43,1087.49 4391.33,1085.22 4380.79,1081.05 4377,1079 4050,890 4254,1942 4050,890 4254,1942 4254,1942 4255.16,1953.27 4254.35,1964.57 4251.59,1975.56 4246.96,1985.9 4240.61,1995.28 4232.72,2003.42 4223.54,2010.06 4213.35,2015.01 4202.45,2018.11 4191.18,2019.27 4179.88,2018.46 4168.89,2015.7 4158.55,2011.07 4149.17,2004.71 4143,1999 3720,1545 3615,1792 3720,1545 3615,1792 3615,1792 3609.64,1801.98 3602.63,1810.88 3594.18,1818.43 3584.55,1824.4 3574.03,1828.6 3562.94,1830.91 3551.61,1831.27 3542,1830 3000,1715 3186,2287 3000,1715 3186,2287 3186,2287 3188.57,2298.03 3189.19,2309.35 3187.84,2320.6 3184.55,2331.44 3179.43,2341.55 3172.64,2350.61 3164.37,2358.36 3154.88,2364.55 3152,2366 2940,2465 3881,3227 2940,2465 3881,3227 3881,3227 3889.14,3234.88 3895.79,3244.06 3900.74,3254.24 3903.85,3265.14 3905.02,3276.41 3904.22,3287.71 3901.46,3298.7 3901,3300 3785,3620 4644,3469 3785,3620 4644,3469 4644,3469 4660.5,3467.63 4676.99,3469.14 4692.97,3473.49 4707.95,3480.55 4721.47,3490.11 4733.13,3501.87 4742.57,3515.47 4749.51,3530.51 4753.73,3546.52 4755.1,3563.03 4755,3567 4710,4430;pc=Transparent;bc=#da291c;"
EndSub

Sub Sprite_Add
  ' Sprite | add shapes to a sprite
  ' param name - sprite name
  ' param shX, shY - origin of shape array
  ' param scale - to resize 
  ' param shape[] - shape array
  ' param nSprite - number of sprite
  ' return nSprite - updated number of sprite
  ' return sprite[] - sprite array
  Stack.PushValue("local", i)
  Stack.PushValue("local", x)
  Stack.PushValue("local", y)
  nSprite = nSprite + 1
  spr = ""
  spr["name"] = name
  spr["x"] = shX
  spr["y"] = shY
  spr["angle"] = 0
  spr["dir"] = 1
  Shapes_CalcWidthAndHeight()
  spr["width"] = shWidth
  spr["height"] = shHeight
  spr["cx"] = shWidth / 2
  spr["cy"] = shHeight / 2
  s = scale
  spr["scale"] = s
  For i = 1 To Array.GetItemCount(shape)
    shp = shape[i]
    GraphicsWindow.PenWidth = shp["pw"] * s
    If shp["pw"] > 0 Then
      GraphicsWindow.PenColor = shp["pc"]
    EndIf
    If Text.IsSubText("rect|ell|tri|poly|text|btn", shp["func"]) Then
      GraphicsWindow.BrushColor = shp["bc"]
    EndIf
    If Text.IsSubText("text|btn", shp["func"]) Then
      If silverlight Then
        fs = Math.Floor(shp["fs"] * 0.9)
      Else
        fs = shp["fs"]
      EndIf
      GraphicsWindow.FontSize = fs * s
      GraphicsWindow.FontName = shp["fn"]
      If shp["fb"] = "False" Then
        GraphicsWindow.FontBold = "False"
      Else
        GraphicsWindow.FontBold = "True"
      EndIf
    EndIf
    If shp["func"] = "rect" Then
      shp["obj"] = Shapes.AddRectangle(shp["width"] * s, shp["height"] * s)
    ElseIf shp["func"] = "ell" Then
      shp["obj"] = Shapes.AddEllipse(shp["width"] * s, shp["height"] * s)
    ElseIf shp["func"] = "tri" Then
      shp["obj"] = Shapes.AddTriangle(shp["x1"] * s, shp["y1"] * s, shp["x2"] * s, shp["y2"] * s, shp["x3"] * s, shp["y3"] * s)
    ElseIf shp["func"] = "poly" Then
      pts = LDText.Split(shp["pt"], " ")
      nPts = Array.GetItemCount(pts)
      Sprite_ScalePoints()
      shp["obj"] = LDShapes.AddPolygon(points)
    ElseIf shp["func"] = "line" Then
      shp["obj"] = Shapes.AddLine(shp["x1"] * s, shp["y1"] * s, shp["x2"] * s, shp["y2"] * s)
    ElseIf shp["func"] = "text" Then
      shp["obj"] = Shapes.AddText(shp["text"])
    EndIf
    x = shp["x"]
    y = shp["y"]
    shp["rx"] = x
    shp["ry"] = y
    If sbd And (shp["func"] = "line") Then
      shp["wx"] = x
      shp["wy"] = y
    ElseIf silverlight And Text.IsSubText("tri|line", shp["func"]) Then
      _alpha = Math.GetRadians(shp["angle"])
      SB_RotateWorkaround()
      shp["wx"] = x
      shp["wy"] = y
    EndIf
    If shp["func"] = "btn" Then
      shp["obj"] = Controls.AddButton(shp["caption"], shX + x * s, shY + y * s)
    Else
      Shapes.Move(shp["obj"], shX + x * s, shY + y * s)
    EndIf
    If Text.IsSubText("rect|ell|tri|poly|text", shp["func"]) And (shp["angle"] <> 0) And (shp["angle"] <> "") Then
      Shapes.Rotate(shp["obj"], shp["angle"])
    EndIf
    shape[i] = shp
  EndFor
  spr["shape"] = shape
  sprite[nSprite] = spr
  y = Stack.PopValue("local")
  x = Stack.PopValue("local")
  i = Stack.PopValue("local")
EndSub

Sub Sprite_ScalePoints
  ' param pts[] - array of points
  ' param nPts - number of points
  ' param s - scale
  ' return points[] - array of scaled points
  points = ""
  pt = ""
  For iPts = 1 To nPts
    _pt = LDText.Split(pts[iPts], ",")
    pt[1] = Math.Round(_pt[1] * s * 100) / 100
    pt[2] = Math.Round(_pt[2] * s * 100) / 100
    points[iPts] = pt
  EndFor
EndSub
      
